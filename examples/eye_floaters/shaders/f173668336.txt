#version 420
layout(triangles, equal_spacing, ccw) in;

in TCS_OUT {
    vec2 position;
    vec2 velocity;
    float pressure;
    float divergence;
    float divergence_linear;
} tes_in[];

out TES_OUT {
    vec3 barycentric;
    flat vec2 velocities[6];
    float pressure;
    flat float divergences[6];
    float divergence_linear;
} tes_out;

void main(void)
{
    float x = gl_TessCoord.x;
    float y = gl_TessCoord.y;
    float z = gl_TessCoord.z;
    tes_out.barycentric = vec3(x,y,z);
    // Pass all the solution data for this patch to the fragment shader.
    for (int i = 0; i < 6; i++) {
        tes_out.velocities[i] = tes_in[i].velocity;
        tes_out.divergences[i] = tes_in[i].divergence;
    }
    gl_Position = vec4(x*tes_in[0].position + y*tes_in[2].position + z*tes_in[4].position, 0,1);
    tes_out.pressure = x*tes_in[0].pressure + y*tes_in[2].pressure + z*tes_in[4].pressure;
    tes_out.divergence_linear = x*tes_in[0].divergence_linear + y*tes_in[2].divergence_linear + z*tes_in[4].divergence_linear;
}

